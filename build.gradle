import groovy.json.JsonSlurper

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.3'
    }
}

plugins {
    id 'application'
    id 'checkstyle'
}

group 'org.javacord'
description 'The Javacord Bot'

sourceCompatibility = 1.8
mainClassName = 'org.javacord.bot.Main'
defaultTasks 'build'

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

ext {
    versions = [
            checkstyle: '8.14',
            javacord  : '3.1.1',
            sdcf4j    : 'v1.0.10',
            okhttp    : '3.9.1',
            jackson   : '2.9.3',
            log4j     : '2.17.1'
    ]
}

dependencies {
    implementation "org.javacord:javacord:${versions.javacord}"
    implementation "de.btobastian.sdcf4j:sdcf4j-javacord:${versions.sdcf4j}"
    implementation "de.btobastian.sdcf4j:sdcf4j-core:${versions.sdcf4j}"
    implementation "com.squareup.okhttp3:okhttp:${versions.okhttp}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}"
    implementation "org.apache.logging.log4j:log4j-api:${versions.log4j}"
    implementation "org.apache.logging.log4j:log4j-jul:${versions.log4j}"

    runtimeOnly "org.apache.logging.log4j:log4j-core:${versions.log4j}"
    runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:${versions.log4j}"
}

tasks.withType(JavaCompile) {
    options.encoding 'UTF-8'
    if (JavaVersion.current().java9Compatible) {
        afterEvaluate {
            options.compilerArgs << '--release' << platform.targetCompatibility.majorVersion
        }
    }
}

checkstyle {
    toolVersion versions.checkstyle
    maxWarnings 0
}

startScripts {
    defaultJvmOpts = ['-Dlog4j.configurationFile=%APP_HOME%/config/log4j2.xml']
    doLast {
        unixScript.text = unixScript.text.replace('%APP_HOME%', '$APP_HOME')
        windowsScript.text = windowsScript.text.replace('%%APP_HOME%%', '%APP_HOME%')
    }
}

distributions {
    main {
        contents {
            from ('src/main/resources/log4j2.xml') {
                into 'config'
            }
        }
    }
}
